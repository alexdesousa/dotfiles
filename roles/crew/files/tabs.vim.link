"""""""""""""""""""""""""
" Sets tab number to tabs

fu! MyTabLabel(n)
    let buflist = tabpagebuflist(a:n)
    let winnr = tabpagewinnr(a:n)
    " Get buffername
    let string = fnamemodify(bufname(buflist[winnr - 1]), ':t')
    let bufdirty = getbufvar(buflist[winnr - 1], "&mod")?'+':''
    return bufdirty.(empty(string) ? '[unnamed]' : string)
endfu

fu! MyTabLine()
    let s = ''
    for i in range(tabpagenr('$'))
    " select the highlighting
        if i + 1 == tabpagenr()
            let s .= '%#TabLineSel#'
        else
            let s .= '%#TabLine#'
        endif

        let s .= ' '. (i+1) . ' '
        let s .= '%{MyTabLabel(' . (i + 1) . ')} '

        if i+1 < tabpagenr('$')
            let s .= ' |'
        endif
    endfor
    return s
endfu

set tabline=%!MyTabLine()
hi TabLineSel term=bold cterm=bold ctermfg=Green
